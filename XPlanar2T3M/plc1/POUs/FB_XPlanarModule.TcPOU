<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_XPlanarModule" Id="{251c3971-244f-091d-3a9c-7a608edd1463}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_XPlanarModule
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	
    XPlanarTable 		: FB_Component_XPlanar 	:= (Name := 'XPlanarTable');
	Station1	 		: FB_ProcessStation		:= (Name := 'Station1');
	Station2	 		: FB_ProcessStation		:= (Name := 'Station2');
	Station3	 		: FB_ProcessStation		:= (Name := 'Station3');

	Movers				: ARRAY[1..Tc3_XPlanarStandard.Params_XPlanar.moverCount] OF FB_XPlanarMover;
	Tracks				: ARRAY[1..Tc3_XPlanarStandard.Params_XPlanar.maxTrackCount] OF FB_XplanarTrackSegment;
	
	MoversTracksInit	: BOOL;	
	MoverErr			: BOOL;
	
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[// CyclicLogic method must be called every PLC cycle - it calls underlying XPlanar cyclic methods
// Execute method must be called when running or moving movers - manages station state machine
// Initialize method assign Mover and Track IPs, configures and builds tracks & track network, sets up Stations
// Reset method resets objects, enables group, enables movers
// Recovery method sorts movers - join track and go to appropriate station
]]></ST>
    </Implementation>
    <Method Name="CyclicLogic" Id="{0677fd3b-e9c5-0389-28b8-94faac1649fd}">
      <Declaration><![CDATA[METHOD CyclicLogic : BOOL
VAR_INPUT
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT MoversTracksInit THEN
	RETURN;
END_IF

XPlanarTable.CyclicLogic();
Station1.CyclicLogic();
Station2.CyclicLogic();
Station3.CyclicLogic();

// Mover error?  (Estopped?)
MoverErr := ip.Movers[1].std.P_ErrorCode<>0 OR ip.Movers[2].std.P_ErrorCode<>0 OR ip.Movers[3].std.P_ErrorCode<>0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute" Id="{6119338a-ec13-0b76-3cb7-2fa2d14031a0}">
      <Declaration><![CDATA[METHOD Execute : BOOL
VAR_INPUT
END_VAR

VAR
    i 				: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Only needs to be called when running or moving movers for recovery - executes station logic

IF XPlanarTable.InitComplete THEN		
	FOR i := 1 TO Params_XPlanar.maxNumStation DO
		ip.Stations[i].Execute();
	END_FOR
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialize" Id="{0f8e7a97-6d35-0e40-0fdb-1e191190ab11}">
      <Declaration><![CDATA[METHOD Initialize : BOOL
VAR_INPUT	
END_VAR

VAR_INST

	i				: UDINT;
	InitState		: UDINT;
	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
CASE InitState OF
	
0:
	InitState := InitState + 10;

10: // assign mover and track IPs
	FOR i := 1 TO Tc3_XPlanarStandard.Params_XPlanar.moverCount DO
		Movers[i].MoverIndex := i;
		ip.Movers[i] := Movers[i];
	END_FOR
	
	FOR i := 1 TO Tc3_XPlanarStandard.Params_XPlanar.maxTrackCount DO
		ip.Tracks[i] := Tracks[i];
	END_FOR
	
	MoversTracksInit := TRUE;
	InitState := InitState + 10;
	
20:
	IF XPlanarTable.InitComplete THEN
		InitState := InitState + 10;
	END_IF	

30:  // Set parameters for all tracks - line segments, points, track start/end relationships
	IF SetupTrackSegments() THEN
		InitState := InitState + 10;
	END_IF

40:  // Build and connect all track segments to create track network
	IF XPlanarTable.BuildTracks() THEN
		InitState := InitState + 10;
	END_IF
	
50:  // Set parameters for all stations
	IF SetupStations() THEN
		InitState := InitState + 10;
	END_IF

60:
	Initialize := TRUE;	

END_CASE

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Recovery" Id="{f1b6e24f-bb5f-05aa-31da-7f94cb063c56}">
      <Declaration><![CDATA[METHOD Recovery : BOOL
VAR_INPUT
END_VAR

VAR_INST
	RecoveryState		: UDINT;
	i					: UDINT;
	JoinTrackNum		: UDINT;
	AllMoversOnTracks	: BOOL;
	GoToStationNum		: UDINT;
	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Recovery := FALSE;

CASE RecoveryState OF
	
0:
	RecoveryState := RecoveryState + 10;

10: // set Mover gap
	FOR i := 1 TO Tc3_XPlanarStandard.Params_XPlanar.moverCount DO
		ip.Movers[i].MoveDynamics.Gap:=MoverGap;	// change default gap
		MoverProcessData[i].Status:=0;	
	END_FOR
	i := 1;
	RecoveryState := 30;

(*  ************* other possible tools / strategies for table recovery ******* 
20: // Dequeue - remove movers from stations
	FOR i := 1 TO Tc3_XPlanarStandard.Params_XPlanar.maxNumStation DO
		ip.Stations[i].MoverQueue.Dequeue();
	END_FOR
	i := 1;
	RecoveryState := 30;
	
	
25: // Station.Recover - get movers back to position within station
	IF ip.Stations[i].Recover() THEN
		i := i + 1;
		IF i > Tc3_XPlanarStandard.Params_XPlanar.maxNumStation THEN
			i := 1;
			RecoveryState := ??;
		END_IF
	END_IF
*)

30: // determine which track to join
	IF ip.Movers[i].std.P_ActPosition.X < 210 THEN
		JoinTrackNum := 1;
	ELSE
		JoinTrackNum := 3;
	END_IF
	
	RecoveryState := RecoveryState + 1;
	
31:	// mover joins track
	IF ip.Movers[i].JoinTrack(ip.Tracks[JoinTrackNum]) THEN
		i := i + 1;
		IF i > Tc3_XPlanarStandard.Params_XPlanar.moverCount THEN
			RecoveryState := RecoveryState + 9;		// all movers handled
		ELSE
			RecoveryState := RecoveryState - 1;		// do next mover
		END_IF
		
	END_IF
	
40:  // verify on track	
	AllMoversOnTracks:=TRUE;
	FOR i := 1 TO Tc3_XPlanarStandard.Params_XPlanar.moverCount DO
//		AllMoversOnTracks := AllMoversOnTracks AND (ip.Movers[i].std.P_TrackId<>0);												// if any mover not on track set False
		AllMoversOnTracks := AllMoversOnTracks AND (ip.Movers[i].std.P_CommandMode = MC_PLANAR_MOVER_COMMAND_MODE.OnTrack);		// if any mover not on track set False
	END_FOR
	
	IF AllMoversOnTracks THEN
		i := 1;
		RecoveryState := RecoveryState + 10;		
	ELSE											// at least on mover not on track
		IF MAIN.HmiDisable THEN
			RecoveryState := 0;						// clear state
		END_IF
	END_IF

50: // determine which station to go to
	IF ip.Movers[i].std.P_ActPosition.Y < 119 THEN
		GoToStationNum := 1;
	ELSIF ip.Movers[i].std.P_ActPosition.X < 210 AND ip.Tracks[3].MoverCount > 1 THEN
		GoToStationNum := 2;
	ELSE
		GoToStationNum := 3;
	END_IF
	
	RecoveryState := RecoveryState + 1;
	
51:	// send movers to stations
	IF ip.Movers[i].SendToStation(ip.Stations[GoToStationNum]) THEN
		i := i + 1;
		IF i > Tc3_XPlanarStandard.Params_XPlanar.moverCount THEN
			RecoveryState := RecoveryState + 9;
		ELSE
			RecoveryState := RecoveryState - 1;		// do next mover
		END_IF
		
	END_IF
	
60:
	Recovery := TRUE;
	RecoveryState := 0;

END_CASE

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{08e72d74-6667-097e-2d50-24e9eee0466e}">
      <Declaration><![CDATA[METHOD Reset : BOOL
VAR_INPUT
END_VAR

VAR_INST
	ResetState		: UDINT;
	
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Reset := FALSE;

CASE ResetState OF
	
0:
	ResetState := 30;

(*	
10:  // Force movers to leave tracks (handles case where movers are still on tracks - support for Recovery method)
	IF XPlanarTable.XPlanarSystem.LeaveTrack() THEN
		ResetState := ResetState + 10;
	END_IF

20:  // Force movers to leave tracks (handles case where movers are still on tracks - support for Recovery method)
	IF XPlanarTable.XPlanarSystem.P_LeaveTrack.Done THEN
		ResetState := ResetState + 10;
	END_IF
*)

30:  // Reset base objects (movers and tracks)
	IF XPlanarTable.Reset() THEN
		ResetState := ResetState + 10;
	END_IF
	
40:  // Enable Group (for CA)
	IF XPlanarTable.EnableGroup() THEN
		ResetState := ResetState + 10;		
	END_IF

50:  // Enable and add movers to Group
	IF XPlanarTable.EnableMovers(UseGroup:=TRUE) THEN
		ResetState := ResetState + 10;		
	END_IF
	
60:  // Enable and add tracks to Group
	IF XPlanarTable.EnableTracks(UseGroup:=TRUE) THEN
		ResetState := ResetState + 10;		
	END_IF	
	
70:
	Reset := TRUE;	
	ResetState := 0;
	
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetupStations" Id="{acfd32ce-c47a-0b97-2922-eb6666a10851}">
      <Declaration><![CDATA[METHOD PRIVATE SetupStations : BOOL
VAR_INPUT
END_VAR

VAR
	nSta			: UDINT;	// station index
	StationSize 	: UDINT := 50;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
nSta := 1;
ip.Stations[nSta] := Station1;							// assign FB to IP
ip.Stations[nSta].ID := nSta;							// set station ID / index
ip.Stations[nSta].TrackID := ip.Tracks[1];				// set rack station is on
ip.Stations[nSta].Position.SetValuesXYC(60, 120, 0);	// station XY location
ip.Stations[nSta].Size.x := StationSize;				// Station size - when station recognizes mover
ip.Stations[nSta].Size.y := StationSize;				
ip.Stations[nSta].StationType := E_StationType.Process;	// (default) process station - Mover stops to 'do' something

GVL.StationProcessData[nSta].VisuPosX:=LREAL_TO_INT(ip.Stations[nSta].Position.x);	// set Visu position
GVL.StationProcessData[nSta].VisuPosY:=LREAL_TO_INT(ip.Stations[nSta].Position.y);

nSta := 2;
ip.Stations[nSta] := Station2;
ip.Stations[nSta].ID := nSta;
ip.Stations[nSta].TrackID := ip.Tracks[2];
ip.Stations[nSta].Position.SetValuesXYC(240, 120, 0);
ip.Stations[nSta].Size.x := StationSize;		
ip.Stations[nSta].Size.y := StationSize;
ip.Stations[nSta].StationType := E_StationType.Process;
ip.Stations[nSta].NextStation := ip.Stations[1];

GVL.StationProcessData[nSta].VisuPosX:=LREAL_TO_INT(ip.Stations[nSta].Position.x);
GVL.StationProcessData[nSta].VisuPosY:=LREAL_TO_INT(ip.Stations[nSta].Position.y);

nSta := 3;
ip.Stations[nSta] := Station3;
ip.Stations[nSta].ID := nSta;
ip.Stations[nSta].TrackID := ip.Tracks[3];
ip.Stations[nSta].Position.SetValuesXYC(420, 120, 0);
ip.Stations[nSta].Size.x := StationSize;
ip.Stations[nSta].Size.y := StationSize;
ip.Stations[nSta].StationType := E_StationType.Process;
ip.Stations[nSta].NextStation := ip.Stations[1];

GVL.StationProcessData[nSta].VisuPosX:=LREAL_TO_INT(ip.Stations[nSta].Position.x);
GVL.StationProcessData[nSta].VisuPosY:=LREAL_TO_INT(ip.Stations[nSta].Position.y);

SetupStations := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetupTrackSegments" Id="{b342f43d-ef70-07fb-31c2-119f04ddb4c4}">
      <Declaration><![CDATA[METHOD PRIVATE SetupTrackSegments : BOOL
VAR_INST
    nSegmentIndex 		: UDINT;
END_VAR

VAR
    ResultAddPoint 		: INT;

END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[SetupTrackSegments := FALSE;
CASE nSegmentIndex OF
    0: //Get ready
        nSegmentIndex := nSegmentIndex + 1;

    //********************************************************************************************************************************
    //Add states for each track segment in the system
    //********************************************************************************************************************************

    1:
        ip.Tracks[nSegmentIndex].ID := nSegmentIndex;
        IF ip.Tracks[nSegmentIndex].TrackTable.ClearTrackTable() THEN
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_Start, E_PointOption.none, 	210.0, 60.0, 0.0);
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_End, E_PointOption.none, 	90.0, 60.0, 0.0);

			ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_Start, E_PointOption.none, 	60.0, 90.0, 0.0);
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_End, E_PointOption.none, 	60.0, 150.0, 0.0);

            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_Start, E_PointOption.none, 	90.0, 180.0, 0.0);
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_End, E_PointOption.none, 	210.0, 180.0, 0.0);

            ip.Tracks[nSegmentIndex].TrackTable.P_CloseTheLoop := FALSE;
            ip.Tracks[nSegmentIndex].TrackTable.P_StartFromTrack := 0;
            ip.Tracks[nSegmentIndex].TrackTable.P_EndAtTrack     := 0;
			
        END_IF

        nSegmentIndex := nSegmentIndex + 1;

    2:
        ip.Tracks[nSegmentIndex].ID := nSegmentIndex;
        IF ip.Tracks[nSegmentIndex].TrackTable.ClearTrackTable() THEN
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_Start, E_PointOption.none, 	240.0, 150.0, 0.0);
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_End, E_PointOption.none, 	240.0, 90.0, 0.0);

            ip.Tracks[nSegmentIndex].TrackTable.P_CloseTheLoop   := FALSE;
            ip.Tracks[nSegmentIndex].TrackTable.P_StartFromTrack := ip.Tracks[1].std;
            ip.Tracks[nSegmentIndex].TrackTable.P_EndAtTrack     := ip.Tracks[1].std;
        END_IF

        nSegmentIndex := nSegmentIndex + 1;

    3:
        ip.Tracks[nSegmentIndex].ID := nSegmentIndex;
        IF ip.Tracks[nSegmentIndex].TrackTable.ClearTrackTable() THEN
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_Start, E_PointOption.none, 	240.0, 180.0, 0.0);
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_End, E_PointOption.none, 	390.0, 180.0, 0.0);

            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_Start, E_PointOption.none, 	420.0, 150.0, 0.0);
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_End, E_PointOption.none, 	420.0, 90.0, 0.0);
			
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_Start, E_PointOption.none, 	390.0, 60.0, 0.0);
            ResultAddPoint := ip.Tracks[nSegmentIndex].TrackTable.AddPoint(E_PointType.Line_End, E_PointOption.none, 	240.0, 60.0, 0.0);

            ip.Tracks[nSegmentIndex].TrackTable.P_CloseTheLoop   := FALSE;
            ip.Tracks[nSegmentIndex].TrackTable.P_StartFromTrack := ip.Tracks[1].std;
            ip.Tracks[nSegmentIndex].TrackTable.P_EndAtTrack     := ip.Tracks[1].std;
        END_IF

        nSegmentIndex := nSegmentIndex + 1;

    4:
        nSegmentIndex      := 0;
        SetupTrackSegments := TRUE;
END_CASE
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>